plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.20.0'
}

android {
    namespace 'com.comapi'
    compileSdk = 33

    defaultConfig {
        minSdk = 16
        targetSdk = 33
    }
    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled = false
        }
        debug {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled = false
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.toVersion("1.8")
        targetCompatibility = JavaVersion.toVersion("1.8")
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    /* testing */
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'org.robolectric:shadows-play-services:3.3.2'
    testImplementation 'org.robolectric:shadows-support-v4:3.3.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.0'
    /* Rx */
    implementation 'io.reactivex:rxjava:1.3.3'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    /* Network */
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    /*FCM*/
    //noinspection BomWithoutPlatform
    implementation 'com.google.firebase:firebase-bom:28.4.0'
    implementation 'com.google.firebase:firebase-messaging:22.0.0'
    implementation 'com.google.android.gms:play-services-base:18.0.1'
    /*Sockets*/
    implementation 'com.neovisionaries:nv-websocket-client:2.3'
}

publishing {

    publications {
        maven(MavenPublication) {

            groupId = 'com.comapi'
            artifactId = 'foundation'
            version = '1.5.2'

            pom {
                name = 'Dotdigital Foundation SDK'
                description = 'Provides the basics of any Dotdigital integration into your apps. The Dotdigital Foundation SDK is responsible for handling: registration, control of User and Device registration details, sending and receiving content & data.'
                url = 'https://dotdigital.com/'
                inceptionYear = '2021'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'marcinswierczek'
                        name = 'Marcin Swierczek'
                        email = 'cpaas.info@dotdigital.com'
                    }
                }
                scm {
                    url = 'https://github.com/comapi/comapi-sdk-android'
                    connection = 'scm:git:https://github.com/comapi/comapi-sdk-android.git'
                    developerConnection = 'scm:git:https://github.com/comapi/comapi-sdk-android.git'
                }
                pom.withXml {
                    def buildNode = asNode().appendNode('build')
                    def pluginsNode = buildNode.appendNode('plugins')
                    def pluginNode = pluginsNode.appendNode('plugin')
                    pluginNode.appendNode('groupId', 'com.simpligility.maven.plugins')
                    pluginNode.appendNode('artifactId', 'android-maven-plugin')
                    pluginNode.appendNode('version', '4.6.0')
                    pluginNode.appendNode('extensions', 'true')
                }
                afterEvaluate {
                    from(components["release"])
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    gitRootSearch = true
    version = '1.5.2'
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'FILE'
        publicKey = 'C:\\Users\\Marcin.Swierczek\\public.pgp'
        secretKey = 'C:\\Users\\Marcin.Swierczek\\private.pgp'
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
    release {
        github {
            skipRelease = true
            skipTag = true
            token = '<skip>'
            commitAuthor {
                name = "MarcinSwierczek"
                email = 'cpaas.info@dotdigital.com'
            }
        }
    }
}